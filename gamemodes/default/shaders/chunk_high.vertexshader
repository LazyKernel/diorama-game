#version 330

layout (location = 0) in uvec3 vertex_position;
layout (location = 1) in vec2 vertex_uv;
layout (location = 2) in vec3 vertex_normal;

layout (location = 3) in uint instance;

uniform mat4 mvp_matrix;
uniform mat4 depth_bias_mvp_matrix;
uniform float time_float;

uniform vec4 light_direction;
uniform vec4 directional_color;
uniform vec4 ambient_color;

uniform float begin_blend_in_distance;
uniform float end_blend_in_distance;
uniform vec3 camera_xyz;

uniform mat4 transforms [6];

out float cos_theta;
out vec2 final_uv;
out vec4 light_rgba;
out vec2 ambient_uv;
out vec4 shadow_coord;

void main()
{
    vec3 instance_translation = vec3 (
            float((instance >> 0u) & 127u) - 32.0, 
            float((instance >> 7u) & 127u) - 32.0, 
            float((instance >> 14u) & 127u) - 32.0);

    uint transform_idx = (instance >> 21) & 7u;
    mat3 rotation = mat3 (transforms [transform_idx]);
    vec3 translation = transforms [transform_idx][3].xyz;

    // blending scale
    float distance_to_camera = length (instance_translation + vec3 (0.5, 0.5, 0.5) - camera_xyz);
    float blend_range = (begin_blend_in_distance - end_blend_in_distance);
    float blend_scale = 1.0 - (clamp (distance_to_camera, end_blend_in_distance, begin_blend_in_distance) - end_blend_in_distance) / blend_range;

    // position
    vec3 vertex_position1 = vertex_position;
    vertex_position1.y *= blend_scale; 
    vec3 vertex_position2 = rotation * vertex_position1 + translation;
    vec3 vertex_position3 = (vertex_position2 / 16.0) + instance_translation;
    gl_Position = mvp_matrix * vec4 (vertex_position3, 1.0);
    gl_Position.w -= 0.00001;

    shadow_coord = depth_bias_mvp_matrix * vec4 (vertex_position3, 1.0);

    // normal transform
    vec3 normal_post_transform = rotation * vertex_normal;
    cos_theta = clamp (dot (vec4 (normal_post_transform.xyz, 0.0), light_direction), 0.0, 1.0);

    // uvs
    final_uv = vertex_uv;

    // directional light
    light_rgba = directional_color * cos_theta;

    // ambient
    //final_rgba = final_rgba + ambient_color;
    light_rgba.a = 1.0;

    ambient_uv.x = time_float;
    ambient_uv.y = 0.5;
}
